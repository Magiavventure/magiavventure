plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco'
	id 'com.github.jmongard.git-semver-plugin' version '0.11.0'
}

semver {
	noDirtyCheck = true
}

group = 'it.magiavventure'
version = semver.version

java {
	sourceCompatibility = '17'
}

tasks.named("jar") {
	enabled = false
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	reports {
		xml.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: [
							'*/magiavventure/**/repository**',
							'*/magiavventure/**/model**',
							'*/magiavventure/**/entity**',
							'*/magiavventure/**/configuration**',
							'*/magiavventure/**/error**',
							'*/magiavventure/**/*Application.class'
					])
		}))
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		url = uri("https://maven.pkg.github.com/Magiavventure/magiavventure-registry")
		credentials {
			username = project.findProperty("user") ?: System.getenv("USERNAME")
			password = project.findProperty("token") ?: System.getenv("TOKEN")
		}
	}
}

ext {
	cacheVersion = '2.0.0'
	commonVersion = '2.0.0'
	jwtVersion = '2.1.0'
	mongoVersion = '2.0.0'
	mapstructVersion = '1.5.5.Final'
}

dependencies {
	implementation "it.magiavventure:cache:$cacheVersion"
	implementation "it.magiavventure:common:$commonVersion"
	implementation "it.magiavventure:jwt:$jwtVersion"
	implementation "it.magiavventure:mongo:$mongoVersion"
	implementation "org.mapstruct:mapstruct:$mapstructVersion"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

build.dependsOn jacocoTestReport